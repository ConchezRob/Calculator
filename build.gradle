plugins {
	id 'java'
	id 'idea' // optional (to generate IntelliJ IDEA project files)
	id 'jacoco'
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation(platform('org.junit:junit-bom:5.10.0'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation("org.assertj:assertj-core:3.26.3")
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.outputLocation=layout.buildDirectory.dir('reports/jacoco/test')
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			includes = ['com.example.*'] // adjust this to your package
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.80.toBigDecimal() // 80% line coverage
			}
			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.70.toBigDecimal() // 70% branch coverage
			}
		}
	}
}

check.dependsOn jacocoTestCoverageVerification